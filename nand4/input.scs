// characterization of a NAND gate with 4 inputs in Sky 130nm

simulator lang=spectre
global 0

include "./models/sky130.scs" section=tt

parameters v0=vdd v1=vdd v2=vdd v3=vdd swpstep=500 \
           vdd=1.8   \
           wn0=4.6   \
           wn1=7.7   \
           wn2=20    \
           wn3=18.5  \
           wp=10     \

save I0,I1,I2,I3,O
global 0

I4 (VDD 0) vsource type=dc dc=vdd

I0 (I0 0) vsource type=dc dc=v0
I1 (I1 0) vsource type=dc dc=v1
I2 (I2 0) vsource type=dc dc=v2
I3 (I3 0) vsource type=dc dc=v3

MN3 (O I3 A 0) sky130_fd_pr__nfet_01v8 m=1 w=wn3 l=0.18
MN2 (A I2 B 0) sky130_fd_pr__nfet_01v8 m=1 w=wn2 l=0.18
MN1 (B I1 C 0) sky130_fd_pr__nfet_01v8 m=1 w=wn1 l=0.18
MN0 (C I0 0 0) sky130_fd_pr__nfet_01v8 m=1 w=wn0 l=0.18
MP0 (O I0 VDD VDD) sky130_fd_pr__pfet_01v8 m=1 w=wp l=0.18
MP1 (O I1 VDD VDD) sky130_fd_pr__pfet_01v8 m=1 w=wp l=0.18
MP2 (O I2 VDD VDD) sky130_fd_pr__pfet_01v8 m=1 w=wp l=0.18
MP3 (O I3 VDD VDD) sky130_fd_pr__pfet_01v8 m=1 w=wp l=0.18

simulatorOptions options psfversion="1.4.0" reltol=1e-3 vabstol=1e-6

options1 options save=selected gmin=0 scale=1u

dc0 dc param=v0 start=0 stop=vdd lin=swpstep
dc1 dc param=v1 start=0 stop=vdd lin=swpstep
dc2 dc param=v2 start=0 stop=vdd lin=swpstep
dc3 dc param=v3 start=0 stop=vdd lin=swpstep